plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.6'
    id 'io.spring.dependency-management' version '1.1.0'
}

group = 'ru.clevertec.ecl'
version = '1.0.0'
sourceCompatibility = '17'
targetCompatibility = '17'

repositories {
    mavenCentral()
    maven { url 'https://artifactory-oss.prod.netflix.net/artifactory/maven-oss-candidates' }
}

ext {
    set('springCloudVersion', "2022.0.2")
}

dependencies {
    // Spring Boot Web
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // AOP
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    // Spring Cloud
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    // DataBase
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'org.postgresql:postgresql'
    implementation 'org.liquibase:liquibase-core'

    // Cache
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-cache'

    // RabbitMQ
    implementation 'org.springframework.boot:spring-boot-starter-amqp'

    // OpenApi
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-api:2.1.0'

    // Lombok and MapStruct
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    implementation 'org.mapstruct:mapstruct:1.5.3.Final'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.1.0'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'

    // Testing
    testImplementation 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.testcontainers:postgresql:1.18.0'
    testImplementation 'com.github.tomakehurst:wiremock-jre8-standalone:2.35.0'
    testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('compileJava') {
    options.compilerArgs = [
            '-Amapstruct.defaultComponentModel=spring',
            '-parameters'
    ]
}

tasks.named('test') {
    useJUnitPlatform()
}
